/*
 * Copyright (c) ProsiebenSat1. Digital GmbH 2019.
 */

android {
    buildTypes {
        libraryVariants.all { variant ->
            task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
                description "Generates Javadoc for $variant.name."
                source = variant.javaCompileProvider.get().source
                destinationDir = file("${buildDir}/javadoc/")
                ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
                doFirst {
                    classpath = files(variant.javaCompileProvider.get().classpath.files) + files(ext.androidJar)
                }
                options {
                    links "http://docs.oracle.com/javase/7/docs/api/"
                    linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
                }
                options.addStringOption('Xdoclint:none', '-quiet')
                exclude '**/BuildConfig.java'
                exclude '**/R.java'
                exclude 'com/google/android/exoplayer/demo/*'
            }
            task("generate${variant.name.capitalize()}JavadocJar", type: Jar, dependsOn: "generate${variant.name.capitalize()}Javadoc") {
                classifier = 'javadoc'
                destinationDir = file("${buildDir}/outputs/javadoc/")
                from file("${buildDir}/javadoc/")
            }
        }
    }
}